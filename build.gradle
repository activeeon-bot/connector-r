apply plugin: 'java'

buildscript {
	repositories {
		if (project.hasProperty('useMavenLocal')) {
			mavenLocal()
		}
		mavenCentral()
		maven { url 'http://repo.activeeon.com/content/repositories/snapshots/' }
		maven { url 'http://repo.activeeon.com/content/repositories/releases/' }
 		maven { url 'http://repo.activeeon.com/content/repositories/thirdparty/' }
		maven { url 'http://spoon.gforge.inria.fr/repositories/releases/' }		
	}
}

subprojects {
	apply plugin: 'base'

	group = 'org.ow2.proactive.r_connector'
	version = '0.9-SNAPSHOT'

	rootProject.buildscript.repositories.each {
        	repositories.add(it)
	}
}

project(':par-script') {
	apply plugin: 'java'

	dependencies {
		compile "org.apache.commons:commons-lang3:3.1"

		compile "org.objectweb.proactive:programming-extension-dataspaces:5.5.0-SNAPSHOT"
		compile "org.ow2.proactive:scheduler-api:3.5.0-SNAPSHOT"
		compile "org.ow2.proactive:scheduler-node:3.5.0-SNAPSHOT"
		compile "org.ow2.proactive:common-api:3.5.0-SNAPSHOT"
		compile "org.ow2.proactive:rm-node:3.5.0-SNAPSHOT"

		compile files("${projectDir}/lib/JRIEngine.jar")
		compile files("${projectDir}/lib/JRI.jar")
		compile files("${projectDir}/lib/JRS.jar")
		compile files("${projectDir}/lib/REngine.jar")
		compile files("${projectDir}/lib/RrserveEngine.jar")	

		testCompile 'junit:junit:4.11'
	}
}

project(':par-connector') {

	configurations {
		copyJars 
	}

	dependencies {
		 copyJars "org.ow2.proactive_grid_cloud_portal:rest-client:1.5.0-SNAPSHOT"
	}

	task copySources(type: Copy) {
		from file("${projectDir}/src/main/r")
		into file("${buildDir}/r-pkg-src")
	}

	task copyJars(type: Copy) {
		from configurations.copyJars
		into file("${buildDir}/r-pkg-src/inst/java")
	}

	task pkg() << {
		def gs = new GroovyShell();
		def source = file("${buildDir}/r-pkg-src").getAbsolutePath()
		def dest = file("${buildDir}").getAbsolutePath()
		def script = file("${projectDir}/src/main/resources/build.groovy")
		new GroovyShell().run(script, [source, dest])
	}

	pkg.dependsOn copySources, copyJars
}


