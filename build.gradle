apply plugin: 'java'

defaultTasks 'clean', 'build'

buildscript {
    repositories {
        if (project.hasProperty('useMavenLocal')) {
            println 'Use mavel local repository'
            mavenLocal()
        }
        mavenCentral()
        maven { url 'http://repo.activeeon.com/content/repositories/snapshots/' }
        maven { url 'http://repo.activeeon.com/content/repositories/releases/' }
        maven { url 'http://repo.activeeon.com/content/repositories/thirdparty/' }
        maven { url 'http://spoon.gforge.inria.fr/repositories/releases/' }
    }
}

allprojects {
    configurations {
        addons
        restJars
    }

    dependencies {
        addons "org.rosuda:jriengine:0.5.x"
        addons "org.rosuda:jri:0.5.x"
        addons "org.rosuda:jrs:28r"
        addons "org.rosuda:rengine:1.7.x"
        addons "org.rosuda:rserveengine:1.7.x"

        restJars 'org.ow2.proactive_grid_cloud_portal:rest-client:1.5.0-SNAPSHOT'
    }
}

subprojects {
    //apply plugin: 'base'
    group = 'org.ow2.proactive.r_connector'
    version = '0.9-SNAPSHOT'

    rootProject.buildscript.repositories.each {
        repositories.add(it)
    }
}

project(':par-script') {
    apply plugin: 'java'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    dependencies {
        compile configurations.addons

        compile 'org.apache.commons:commons-lang3:3.1'

        compile 'org.objectweb.proactive:programming-extension-dataspaces:5.5.0-SNAPSHOT'
        compile 'org.ow2.proactive:scheduler-api:3.5.0-SNAPSHOT'
        compile 'org.ow2.proactive:scheduler-node:3.5.0-SNAPSHOT'
        compile 'org.ow2.proactive:common-api:3.5.0-SNAPSHOT'
        compile 'org.ow2.proactive:rm-node:3.5.0-SNAPSHOT'

        testCompile 'junit:junit:4.11'
    }

    // This task should work even if the Environement variables are not set
    test {
        forkEvery 1
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = 'full'

        afterTest { desc, result ->
            println '------> Executed test ' + desc.name + ' ['+desc.className + '] with result: ' + result.resultType
        }
    }
}

project(':par-connector') {

    def noScheduler = System.getProperty('noScheduler') != null

    def isWindows = System.properties['os.name'].toLowerCase().contains('windows')
    def isMac = System.properties['os.name'].toLowerCase().contains('mac')
    def isLinux = System.properties['os.name'].toLowerCase().contains('linux')
    def jHome = System.getenv('JAVA_HOME')
    def rHome = System.getenv('R_HOME')
    def rLibs = System.getenv('R_LIBS')
    def schedHome = System.getenv('SCHEDULER_HOME')

    def rExec
    def rScript
    def env = [:]
    def schedProcess

    task propertyCheck(dependsOn: [project(':par-script').test, project(':par-script').build]) << {
        println 'Checking required environment variables'
        assert jHome != null : 'Environment variable JAVA_HOME is not set'
        assert file(jHome).exists() != null : 'The path given by JAVA_HOME does not exists'
        assert rHome != null : 'Environment variable R_HOME is not set'
        assert rLibs != null : 'Environment variable R_LIBS is not set' // todo this should be optional
        assert schedHome != null : 'Environment variable SCHEDULER_HOME is not set'
    }

    task setEnv(dependsOn: propertyCheck) << {
        System.getenv().each() { k,v ->
            env.put(k, v)
        }
        if (!isMac) {
            env.put('JAVA_HOME', env.get('JAVA_HOME') + File.separator + 'jre')
        }
        if (isLinux) {
            def ldLib
            if (System.getProperty('os.arch').contains('64')) {
                ldLib = new File(env.get('JAVA_HOME'), 'lib/amd64/server')
            } else {
                ldLib = new File(env.get('JAVA_HOME'), 'lib/i386/client')
            }
            assert ldLib.exists() : 'Cannot locate LD_LIBRARY_PATH ' + ldLib.getAbsolutePath()
            env.put('LD_LIBRARY_PATH', ldLib.getAbsolutePath())
        }

    }

    task setRExec(dependsOn: setEnv) << {
        if (isWindows) {
            def ARCH = System.getenv('ProgramFiles(x86)') != null ? 'x64' : 'i386'
            rExec = rHome + File.separator + 'bin' + File.separator + ARCH + File.separator + 'R.exe'
            rScript = rHome + File.separator + 'bin' + File.separator + ARCH + File.separator + 'Rscript.exe'
        } else {
            rExec = rHome + File.separator + 'bin' + File.separator + 'R'
            rScript = rHome + File.separator + 'bin' + File.separator + 'Rscript'
        }
    }

    task copyLibs(dependsOn: setRExec) << {
        copy {
            from configurations.restJars
            into "${buildDir}/dependency-libs"
        }
    }

    task pkg(dependsOn: copyLibs) << {

        def rSrc = "${buildDir}/r-pkg-src"
        // copy source
        copy {
            from "${projectDir}/src/main/r"
            into rSrc
        }
        // copy jars
        copy {
            from configurations.restJars
            into "${rSrc}/inst/java"
        }

        println 'Checking R add-on package ...'
        exec {
            workingDir buildDir
            commandLine rExec, 'CMD', 'check', '--no-codoc', '--no-manual', '--no-multiarch', 'PARConnector'
            environment env
        }

        println 'Building R add-on package ...'
        exec {
            workingDir buildDir
            commandLine rExec, 'CMD', 'INSTALL', '--build', '--preclean', '--with-keep.source', '--no-multiarch', rSrc
            environment env
        }
    }

    task reinstallRPackage(dependsOn: pkg) << {
        // TODO: IF R_LIBS IS NOT DEFINED
        // build r package
        //def rLibs
        if (isWindows) {
            rLibs = new File(rHome, 'library')
        } else  {
            rLibs = file(rLibs)
        }
        assert rLibs.exists() : 'Unable to locate r libs'

        def p = file(buildDir).listFiles().find { it.name.endsWith('.tar.gz') ||  it.name.endsWith('.tgz') || it.name.endsWith('.zip') }
        assert p.exists() : 'Cannot locate PARConnector installation package.'

        println 'Removing previous installation'
        exec {
            workingDir buildDir
            commandLine rExec, 'CMD', 'REMOVE', '--library=' + rLibs.getAbsolutePath() , 'PARConnector'
            environment env
        }

        println 'Reinstallling'
        exec {
            workingDir buildDir
            commandLine rExec, 'CMD', 'INSTALL', '--no-multiarch', '--library=' + rLibs.getAbsolutePath(), p.getAbsolutePath()
            environment env
        }
    }

    task installJars(dependsOn: reinstallRPackage) << {
        assert project(':par-script').jar.archivePath.exists() : "par-script jar file does not exist."
        copy {
            println "Installing jars from: ${rootDir}/par-script/build/libs/"
            from project(':par-script').jar.archivePath
            from configurations.addons
            into "${schedHome}/addons"
        }
    }

    task startScheduler(dependsOn: reinstallRPackage) << {
        if (noScheduler) {
            return
        }
        schedProcess = ['jrunscript', 'start-server.js'].execute(null, new File(schedHome, 'bin'))
        try {
            schedProcess.inputStream.eachLine {
                println '>> ' + it
                if (it.contains('terminate all')) {
                    throw new Exception()
                }
            }
        } catch (e) {}

        try {
            int value = schedProcess.exitValue();
            if (value != 0) {
                throw new Exception('Could not start the Scheduler, start-server.js exited with code ' + value);
            }
        } catch (IllegalThreadStateException e) {/* the process is still running */}
    }

    task runFuncTests(dependsOn: startScheduler) << {
        def funcTestsDir = file("${projectDir}/src/main/test/r/functionalTests")
        funcTestsDir.listFiles().each {
            if (it.getName().startsWith('test')) {
                println '\n######################\n#   RUNNING functional test ' + it + ' ... \n######################'
                def toRun = ""+it.getAbsolutePath()
                exec {
                    workingDir funcTestsDir
                    //commandLine rExec, '--quiet' ,'--vanilla', '--args', 'QUITONERROR', '<', it
                    commandLine rScript, '--slave', '--verbose', '--no-save', '--no-restore-history', toRun, 'QUITONERROR'
                    environment env
                }
            }
        }
    }

    task stopScheduler(dependsOn: runFuncTests) << {
        if (noScheduler) {
            return;
        }
        println '\n######################\n#   SHUTTING down the Scheduler ... \n######################'
        try {
            schedProcess.out << 'exit\n';
            schedProcess.out.close();
        } catch (e) {e.printStackTrace()}
        schedProcess.waitFor();
    }

    task build(dependsOn: stopScheduler)

    //pkg.dependsOn
    //pkg.dependsOn propertyCheck, setEnv, setRExec
    //test.dependsOn pkg, //reinstallRPackage, installJars, startScheduler, runFuncTests, stopScheduler
}